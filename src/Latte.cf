-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClassDef.  TopDef ::= "class" Ident "{" [ClassPole] "}";

ClassDefExt. TopDef ::= "class" Ident "extends" Ident "{" [ClassPole] "}" ;

ClassFieldDef. ClassPole ::= Type Ident ";" ;

ClassMethodDef. ClassPole ::= Type Ident "(" [Arg] ")" Block ;

separator nonempty TopDef "" ;

separator nonempty ClassPole "" ;

Arg. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= LValue "=" Expr  ";" ;

Incr.      Stmt ::= LValue "++"  ";" ;

Decr.      Stmt ::= LValue "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

-- ForArray.      Stmt ::= "for" "(" Type Ident ":" Ident ")" ;

SExp.      Stmt ::= Expr  ";" ;

LValue.    LValue ::= Ident ;

LValueClassField. LValue ::= LValue "." Ident ;

-- LValueArrayElem. LValue ::= LValue "[" Expr "]" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Boolean.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

-- ArrType.       Type ::= Type "[]";

ClassType. Type ::= Ident ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------


ELValue.    Expr8 ::= LValue ;

ELitInt.   Expr8 ::= Integer ;

ELitTrue.  Expr8 ::= "true" ;

ELitFalse. Expr8 ::= "false" ;

EApp.      Expr8 ::= LValue "(" [Expr] ")" ;

EString.   Expr8 ::= String ;

-- ENewArray. Expr7 ::= "new" Type "[" Expr "]" ;

ENew.      Expr7 ::= "new" Type ;

ENullCast.     Expr6 ::= "(" Type ")null" ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 8 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
